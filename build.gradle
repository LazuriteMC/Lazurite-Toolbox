plugins {
    id 'architectury-plugin' version "$architectury_plugin_version"
    id 'dev.architectury.loom' version "$loom_version"
    id 'maven-publish'
}

version mod_version
group maven_group

loom.silentMojangMappingsLicense()
architectury.common()

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }

    withSourcesJar()
}

dependencies {
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:$fabric_loader_version"
}

tasks.withType(ProcessResources) {
    inputs.property "version", version

    filesMatching "fabric.mod.json", {
        expand "version": version
    }
}

transformProductionFabric {
    archiveClassifier = ''
}

transformProductionForge {
    archiveClassifier = ''
}

publishing {
    repositories {
        maven {
            name 'Lazurite'
            url 'https://lazurite.dev/releases'

            credentials {
                username property('publish.lazurite.alias')
                password property('publish.lazurite.secret')
            }
        }
    }

    publications {
        forge(MavenPublication) {
            artifactId = 'toolbox-forge'

            artifact (transformProductionForge) {
                builtBy transformProductionForge
            }
        }

        fabric(MavenPublication) {
            artifactId = 'toolbox-fabric'

            artifact (transformProductionFabric) {
                builtBy transformProductionFabric
            }
        }
    }
}