plugins {
	id "fabric-loom" version "${loom_version}"
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

allprojects { project ->
	apply plugin: 'fabric-loom'
	apply plugin: 'maven-publish'
	apply plugin: 'java-library'

	dependencies {
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings minecraft.officialMojangMappings()
		modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
		modImplementation(fabricApi.module("fabric-api-base", project.fabric_version))
	}

	archivesBaseName = project.archives_base_name
	version = project.mod_version
	group = project.maven_group

	processResources {
		inputs.property "version", project.version

		filesMatching("fabric.mod.json") {
			expand "version": project.version
		}
	}

	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"
		it.options.release = 16
	}

	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = "sources"
		from sourceSets.main.allSource
	}

	java {
		withSourcesJar()
	}

	jar {
		from("LICENSE") {
			rename { "${it}_${project.archivesBaseName}"}
		}
	}

	remapJar {
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				artifactId = 'toolbox'

				pom {
					name = 'Lazurite Toolbox'
					description = 'A set of utils used by several Lazurite mods.'
					url = 'https://github.com/LazuriteMC/Lazurite-Toolbox'
					developers {
						developer {
							id = 'bluevista'
							name = 'BlueVista'
							email = 'ethan@ejohnsons.com'
						}
					}
				}

				versionMapping {
					usage('java-api') {
						fromResolutionOf('runtimeClasspath')
					}
					usage('java-runtime') {
						fromResolutionResult()
					}
				}

				artifact(remapJar) {
					builtBy remapJar
				}

				artifact(sourcesJar) {
					builtBy remapSourcesJar
				}
			}
		}

		repositories {
			maven {
				url = 'https://lazurite.dev/maven/releases'
				credentials {
					username = project.property("publish.lazurite.alias")
					password = project.property("publish.lazurite.secret")
				}
			}
		}
	}
}