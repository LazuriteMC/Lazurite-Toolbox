plugins {
    id 'com.github.johnrengelman.shadow' version "$shadow_version"
    id 'architectury-plugin' version "$architectury_plugin_version"
    id 'dev.architectury.loom' version "$loom_version"
    id 'maven-publish'
}

allprojects {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    loom.silentMojangMappingsLicense()
    version mod_version
    group maven_group

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }

        withSourcesJar()
    }

    dependencies {
        minecraft "com.mojang:minecraft:$minecraft_version"
        mappings loom.officialMojangMappings()
    }

    publishing {
        repositories {
            maven {
                name 'Lazurite'
                url 'https://lazurite.dev/releases'

                credentials {
                    username property('publish.lazurite.alias')
                    password property('publish.lazurite.secret')
                }
            }
        }

        publications {
            "$name"(MavenPublication) {
                artifactId = archivesBaseName
                from components.java
            }
        }
    }
}